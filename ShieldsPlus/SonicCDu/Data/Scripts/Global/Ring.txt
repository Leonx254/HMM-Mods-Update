//-------------------Sonic CD Ring Script---------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

//-------Aliases-------//
#alias Object.Value2	:	Object.XVelocity
#alias Object.Value3	:	Object.YVelocity
#alias Object.PropertyValue	:	Object.DrawingPlane

// Player Alias
#alias Player.Value0	:	Player.Rings

#alias 0	:	RING_IDLE
#alias 1	:	RING_MAGNET

// Drawing Plane Aliases
#alias 0	:	DRAWINGPLANE_A
#alias 1	:	DRAWINGPLANE_B

// Pan
#alias 0	:	PAN_RIGHT
#alias 1	:	PAN_LEFT

// Player
#alias 0	:	PLAYER_SONIC

// Game Mode Aliases
#alias 2	:	MODE_TIMEATTACK

// Global SFX
#alias 20	:	SFX_G_1UP
#alias 21	:	SFX_G_ACHIEVEMENT

#alias 4	:	SHIELD_ELECTRIC


sub ObjectPlayerInteraction
	if Player.State != Player_State_Hurt
		if Player.CollisionPlane == Object.PropertyValue

			PlayerObjectCollision(C_TOUCH, -8, -8, 8, 8)
			if CheckResult == true
				Object.Type = TypeName[Ring Sparkle]
				Player.Rings++
				if Player.Rings > 999
					Player.Rings = 999
				end if
				
				if Player.Rings >= Ring.ExtraLife
					if Options.GameMode != MODE_TIMEATTACK
#platform: Use_Origins
						if game.coinMode == false
							Player.Lives++
						else
							game.callbackParam0 = 1	// Set the amount of coins to add
							EngineCallback(NOTIFY_ADD_COIN)
						end if
#endplatform

#platform: Use_Standalone
						Player.Lives++
#endplatform

						if Stage.PlayerListPos == PLAYER_SONIC
							PlaySfx(SFX_G_1UP, false)
						else
							PlaySfx(SFX_G_ACHIEVEMENT, false)
						end if
					end if

					Ring.ExtraLife += 100

					// You can only achieve 3 extra lives via rings
					if Ring.ExtraLife > 300
						Ring.ExtraLife = 1000
					end if

					// King of the Rings achievement criteria
					if Player.Rings >= 200
						if Stage.PlayerListPos == PLAYER_SONIC
							if Stage.DebugMode == false
								SetAchievement(4, 100)
							end if
						end if
					end if
				end if

				if Ring.Pan == PAN_RIGHT
					SetSfxAttributes(1, -1, -100)
					Ring.Pan = PAN_LEFT
				else
					SetSfxAttributes(2, -1, 100)
					Ring.Pan = PAN_RIGHT
				end if
			else
				if Object.State == RING_IDLE
					if Player.Shield == SHIELD_ELECTRIC
						PlayerObjectCollision(C_TOUCH, -64, -64, 64, 64)
						if CheckResult == true
							Object.State = RING_MAGNET
						end if
					end if
				end if
			end if
		end if
	end if
	
	if Object.State == RING_MAGNET
		if Player.Shield != SHIELD_ELECTRIC
			Object.Type = TypeName[Lose Ring]
			Object.AnimationSpeed = 128
			Object.Alpha = 256
		else
			if Object.XPos > Player.XPos
				if Object.XVelocity > 0
					Object.XVelocity -= 49152
				else
					Object.XVelocity -= 12288
				end if
			else
				if Object.XVelocity < 0
					Object.XVelocity += 49152
				else
					Object.XVelocity += 12288
				end if
			end if
			if Object.YPos > Player.YPos
				if Object.YVelocity > 0
					Object.YVelocity -= 49152
				else
					Object.YVelocity -= 12288
				end if
			else
				if Object.YVelocity < 0
					Object.YVelocity += 49152
				else
					Object.YVelocity += 12288
				end if
			end if
			Object.XPos += Object.XVelocity
			Object.YPos += Object.YVelocity
		end if
	end if
end sub


sub ObjectDraw
	DrawSprite(Ring.Frame)
end sub


sub ObjectStartup
	LoadSpriteSheet("Global/Items.gif")

	SpriteFrame(-8, -8, 16, 16, 1, 1)		// #0 - Ring Frame 0
	SpriteFrame(-8, -8, 16, 16, 1, 18)		// #1 - Ring Frame 1
	SpriteFrame(-8, -8, 16, 16, 1, 35)		// #2 - Ring Frame 2
	SpriteFrame(-8, -8, 16, 16, 1, 52)		// #3 - Ring Frame 3
	SpriteFrame(-8, -8, 16, 16, 1, 69)		// #4 - Ring Frame 4
	SpriteFrame(-8, -8, 16, 16, 1, 86)		// #5 - Ring Frame 5
	SpriteFrame(-8, -8, 16, 16, 1, 103)		// #6 - Ring Frame 6
	SpriteFrame(-8, -8, 16, 16, 1, 120)		// #7 - Ring Frame 7

	// Used to be below LoadSpriteSheet, moved here for consistency
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Ring]
			if Object[ArrayPos0].DrawingPlane == DRAWINGPLANE_A
				Object[ArrayPos0].DrawOrder = 4
			else
				Object[ArrayPos0].DrawOrder = 2
			end if
		end if
		ArrayPos0++
	loop
end sub

sub RSDK
	LoadSpriteSheet("Global/Display.gif")
	SetEditorIcon(Icon0,SingleIcon,-16,-16,32,32,1,143)
end sub
