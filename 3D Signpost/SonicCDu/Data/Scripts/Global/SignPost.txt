//-----------------Sonic CD Sign Post Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0		:	Object.Timer
#alias Object.Direction		:	Object.FrameTimer

#alias Object.Value2		:	Object.XCenter
#alias Object.Value3		:	Object.YCenter
#alias Object.Value4		:	Object.ZCenter
#alias Object.Value5		:	Object.pointX
#alias Object.Value6		:	Object.pointY
#alias Object.Value7		:	Object.pointZ

#alias Object.Value1		:	Object.rotIncrease
#alias Object.Rotation		:	Object.angle
#alias Object.Scale			:	Object.SpinRot

// Player Aliases
#alias Player.Value4		:	Player.InvincibleTimer

// ActFinish Aliases
#alias Object[30].DrawOrder	:	ActFinish.DrawOrder
#alias Object[30].Priority	:	ActFinish.Priority

// States
#alias 0	:	SIGNPOST_IDLE
#alias 1	:	SIGNPOST_FLIP
#alias 2	:	SIGNPOST_MOVE_PLAYER
#alias 3	:	SIGNPOST_STOP_PLAYER
#alias 4	:	SIGNPOST_BLANK

// Control Mode
#alias -1	:	CONTROLMODE_NONE

// Player
#alias 0	:	PLAYER_SONIC_A
#alias 1	:	PLAYER_TAILS_A

// Global SFX
#alias 15	:	SFX_G_SIGNPOST

// Property Values
#alias 0	:	UPDATE_TILELAYER_1
#alias 1	:	UPDATE_TILELAYER_2
#alias 2	:	COMMON_SIGN
#alias 3	:	HANDLE_MULTIPLE_SIGNS

// Priority
#alias 1	:	PRIORITY_ACTIVE
#alias 2 : PRIORITY_ALWAYS         // Object will always update
#alias 3	:	PRIORITY_XBOUNDS

// Face Aliases
#alias 0 : FACE_TEXTURED_3D
#alias 1 : FACE_TEXTURED_2D
#alias 2 : FACE_COLOURED_3D
#alias 3 : FACE_COLOURED_2D

#function SignPost_GetBoxCenter
#function SignPost_DrawBackFace
#function SignPost_DrawFrontFace
#function SignPost_DrawLeftFace
#function SignPost_DrawRightFace
#function SignPost_DrawTopSideFace

function SignPost_GetBoxCenter
	// Get the center of the object
	object.XCenter = 0
	object.YCenter = 0
	object.ZCenter = 0
	
	ArrayPos0 = 0
	while ArrayPos0 < 8
		switch ArrayPos0
		case 0
			object.ZCenter +=   0
			object.YCenter +=   0
			object.XCenter += -28
			break
		case 1
			object.ZCenter +=   8
			object.YCenter +=   0
			object.XCenter += -28
			break
		case 2
			object.ZCenter +=   8
			object.YCenter +=  64
			object.XCenter += -28
			break
		case 3
			object.ZCenter +=   0
			object.YCenter +=  64
			object.XCenter += -28
			break

		case 4
			object.ZCenter +=  0
			object.YCenter +=  0
			object.XCenter += 28
			break
		case 5
			object.ZCenter +=  8
			object.YCenter +=  0
			object.XCenter += 28
			break
		case 6
			object.ZCenter +=  8
			object.YCenter += 64
			object.XCenter += 28
			break
		case 7
			object.ZCenter +=  0
			object.YCenter += 64
			object.XCenter += 28
			break
		end switch
		ArrayPos0++
	loop
	object.XCenter /= 8
	object.YCenter /= 8
	object.ZCenter /= 8
end function

function SignPost_GetRotation
	arrayPos0 = 0
	while arrayPos0 < 8
		switch arrayPos0
		case 0
			object.pointZ =   0
			object.pointY =   0
			object.pointX = -28
			break
		case 1
			object.pointZ =   8
			object.pointY =   0
			object.pointX = -28
			break
		case 2
			object.pointZ =   8
			object.pointY =  64
			object.pointX = -28
			break
		case 3
			object.pointZ =   0
			object.pointY =  64
			object.pointX = -28
			break

		case 4
			object.pointZ =  0
			object.pointY =  0
			object.pointX = 28
			break
		case 5
			object.pointZ =  8
			object.pointY =  0
			object.pointX = 28
			break
		case 6
			object.pointZ =  8
			object.pointY = 64
			object.pointX = 28
			break
		case 7
			object.pointZ =  0
			object.pointY = 64
			object.pointX = 28
			break
		end switch

		// Rotate X
		// X
		TempValue0   = object.pointX
		TempValue0  -= object.XCenter
		cos(object.angle, object.SpinRot)
		TempValue0  *= object.angle

		TempValue1   = object.pointZ
		TempValue1  -= object.ZCenter
		sin(object.angle, object.SpinRot)
		TempValue1  *= object.angle
		TempValue2  = TempValue0
		TempValue2 -= TempValue1

		// Z
		TempValue0   = object.pointX
		TempValue0  -= object.XCenter
		Sin(object.angle, object.SpinRot)
		TempValue0  *= object.angle

		TempValue1   = object.pointZ
		TempValue1  -= object.ZCenter
		Cos(object.angle, object.SpinRot)
		TempValue1  *= object.angle
		TempValue3  = TempValue0
		TempValue3 += TempValue1


		TempValue2 += object.XCenter
		TempValue3 += object.ZCenter
		TempValue2 >>= 8
		TempValue3 >>= 8
		
		VertexBuffer[arrayPos0].x  = object.XCenter
		VertexBuffer[arrayPos0].x += TempValue2
		VertexBuffer[arrayPos0].z  = object.ZCenter
		VertexBuffer[arrayPos0].z += TempValue3
		VertexBuffer[arrayPos0].y  = object.pointY
		switch arrayPos0
		case 2
		case 3
		case 6
		case 7
			VertexBuffer[arrayPos0].y -= 3
			break
		end switch
		ArrayPos0++
	loop
end function

function SignPost_DrawBackFace
	VertexBuffer[8].x  = VertexBuffer[0].x
	VertexBuffer[8].y  = VertexBuffer[0].y
	VertexBuffer[8].z  = VertexBuffer[0].z

	VertexBuffer[9].x  = VertexBuffer[4].x
	VertexBuffer[9].y  = VertexBuffer[4].y
	VertexBuffer[9].z  = VertexBuffer[4].z

	VertexBuffer[10].x = VertexBuffer[5].x
	VertexBuffer[10].y = VertexBuffer[5].y
	VertexBuffer[10].z = VertexBuffer[5].z

	VertexBuffer[11].x = VertexBuffer[1].x
	VertexBuffer[11].y = VertexBuffer[1].y
	VertexBuffer[11].z = VertexBuffer[1].z
end function

function SignPost_DrawFrontFace
	VertexBuffer[12].x  = VertexBuffer[3].x
	VertexBuffer[12].y  = VertexBuffer[3].y
	VertexBuffer[12].z  = VertexBuffer[3].z
	VertexBuffer[12].x += 1

	VertexBuffer[13].x  = VertexBuffer[2].x
	VertexBuffer[13].y  = VertexBuffer[2].y
	VertexBuffer[13].z  = VertexBuffer[2].z
	VertexBuffer[13].x += 1

	VertexBuffer[14].x  = VertexBuffer[6].x
	VertexBuffer[14].y  = VertexBuffer[6].y
	VertexBuffer[14].z  = VertexBuffer[6].z
	VertexBuffer[14].x -= 1

	VertexBuffer[15].x  = VertexBuffer[7].x
	VertexBuffer[15].y  = VertexBuffer[7].y
	VertexBuffer[15].z  = VertexBuffer[7].z
	VertexBuffer[15].x -= 1
end function

function SignPost_DrawLeftFace
	VertexBuffer[16].x  = VertexBuffer[1].x
	VertexBuffer[16].y  = VertexBuffer[1].y
	VertexBuffer[16].z  = VertexBuffer[1].z

	VertexBuffer[17].x  = VertexBuffer[5].x
	VertexBuffer[17].y  = VertexBuffer[5].y
	VertexBuffer[17].z  = VertexBuffer[5].z

	VertexBuffer[18].x  = VertexBuffer[6].x
	VertexBuffer[18].y  = VertexBuffer[6].y
	VertexBuffer[18].y += 5
	VertexBuffer[18].z  = VertexBuffer[6].z

	VertexBuffer[19].x  = VertexBuffer[2].x
	VertexBuffer[19].y  = VertexBuffer[2].y
	VertexBuffer[19].y += 5
	VertexBuffer[19].z  = VertexBuffer[2].z
end function

function SignPost_DrawRightFace
	VertexBuffer[20].x  = VertexBuffer[0].x
	VertexBuffer[20].y  = VertexBuffer[0].y
	VertexBuffer[20].z  = VertexBuffer[0].z

	VertexBuffer[21].x  = VertexBuffer[3].x
	VertexBuffer[21].y  = VertexBuffer[3].y
	VertexBuffer[21].y += 5
	VertexBuffer[21].z  = VertexBuffer[3].z

	VertexBuffer[22].x  = VertexBuffer[7].x
	VertexBuffer[22].y  = VertexBuffer[7].y
	VertexBuffer[22].y += 5
	VertexBuffer[22].z  = VertexBuffer[7].z

	VertexBuffer[23].x  = VertexBuffer[4].x
	VertexBuffer[23].y  = VertexBuffer[4].y
	VertexBuffer[23].z  = VertexBuffer[4].z
end function

function SignPost_DrawTopSideFace
	// Get the center of the object
	object.XCenter = 0
	object.YCenter = 0
	object.ZCenter = 0
	
	ArrayPos0 = 0
	while ArrayPos0 < 8
		switch ArrayPos0
		case 0
			object.ZCenter +=  0
			object.YCenter +=  62
			object.XCenter +=-2
			break
		case 1
			object.ZCenter +=  8
			object.YCenter +=  62
			object.XCenter +=-2
			break
		case 2
			object.ZCenter +=  8
			object.YCenter += 66
			object.XCenter +=-2
			break
		case 3
			object.ZCenter +=  0
			object.YCenter += 66
			object.XCenter +=-2
			break
		case 4
			object.ZCenter +=  0
			object.YCenter +=  62
			object.XCenter += 2
			break
		case 5
			object.ZCenter +=  8
			object.YCenter +=  62
			object.XCenter += 2
			break
		case 6
			object.ZCenter +=  8
			object.YCenter += 66
			object.XCenter += 2
			break
		case 7
			object.ZCenter +=  0
			object.YCenter += 66
			object.XCenter += 2
			break
		end switch
		ArrayPos0++
	loop
	object.XCenter /= 8
	object.YCenter /= 8
	object.ZCenter /= 8
	
	arrayPos0 = 0
	while arrayPos0 < 8
		switch arrayPos0
		case 0
			object.pointZ =  0
			object.pointY = 62
			object.pointX = -2
			break
		case 1
			object.pointZ =  8
			object.pointY = 62
			object.pointX = -2
			break
		case 2
			object.pointZ =  8
			object.pointY = 66
			object.pointX = -2
			break
		case 3
			object.pointZ =  0
			object.pointY = 66
			object.pointX = -2
			break
		case 4
			object.pointZ =  0
			object.pointY = 62
			object.pointX =  2
			break
		case 5
			object.pointZ =  8
			object.pointY = 62
			object.pointX =  2
			break
		case 6
			object.pointZ =  8
			object.pointY = 66
			object.pointX =  2
			break
		case 7
			object.pointZ =  0
			object.pointY = 66
			object.pointX =  2
			break
		end switch

		// Rotate X
		// X
		TempValue0   = object.pointX
		TempValue0  -= object.XCenter
		cos(object.angle, object.SpinRot)
		TempValue0  *= object.angle

		TempValue1   = object.pointZ
		TempValue1  -= object.ZCenter
		sin(object.angle, object.SpinRot)
		TempValue1  *= object.angle
		TempValue2   = TempValue0
		TempValue 2 -= TempValue1

		// Y
		TempValue0   = object.pointX
		TempValue0  -= object.XCenter
		Sin(object.angle, object.SpinRot)
		TempValue0  *= object.angle

		TempValue1   = object.pointZ
		TempValue1  -= object.ZCenter
		Cos(object.angle, object.SpinRot)
		TempValue1  *= object.angle
		TempValue3   = TempValue0
		TempValue3  += TempValue1


		TempValue2  += object.XCenter
		TempValue3  += object.ZCenter
		TempValue2 >>= 8
		TempValue3 >>= 8
		
		ArrayPos1  = arrayPos0
		ArrayPos1 += 24
		VertexBuffer[ArrayPos1].x  = object.XCenter
		VertexBuffer[ArrayPos1].x += TempValue2
		VertexBuffer[ArrayPos1].z  = object.ZCenter
		VertexBuffer[ArrayPos1].z += TempValue3
		VertexBuffer[ArrayPos1].y  = object.pointY
		ArrayPos0++
	loop
end function

sub ObjectMain
	switch Object.State
	case SIGNPOST_IDLE
		Object.Frame = 1

		TempValue0  = Object.XPos
		TempValue0 -= 0x1000000
		if Player.XPos > TempValue0
			Stage.NewYBoundary1   = Object.YPos
			Stage.NewYBoundary1 >>= 16
			Stage.NewYBoundary1  -= Screen.CenterY
			Stage.NewYBoundary2   = Object.YPos
			Stage.NewYBoundary2 >>= 16
			Stage.NewYBoundary2  += Screen.CenterY
			Stage.NewYBoundary2  -= 32
		end if
		TempValue0  = Object.YPos
		TempValue0 += 0x600000
		if Player.YPos < TempValue0	// Makes sure the player triggers the signpost no matter the height
			Stage.NewXBoundary1   = Object.XPos
			Stage.NewXBoundary1 >>= 16
			Stage.NewXBoundary1  -= Screen.CenterX
			Stage.NewXBoundary1  -= Screen.CenterX

			Stage.NewXBoundary2   = Object.XPos
			Stage.NewXBoundary2 >>= 16
			Stage.NewXBoundary2  += Screen.CenterX

			if Player.XPos > Object.XPos	// Check that the player crossed the sign post
				Stage.NewXBoundary1   = Object.XPos
				Stage.NewXBoundary1 >>= 16
				Stage.NewXBoundary1  -= Screen.CenterX
#platform: Use_Origins
				EngineCallback(NOTIFY_TOUCH_SIGNPOST)
#endplatform
				Stage.TimeEnabled = false

				Object.State = SIGNPOST_FLIP
				object.rotIncrease = 32
				Object.Timer = 8

				PlaySfx(SFX_G_SIGNPOST, false)
			end if
		end if
		break

	case SIGNPOST_FLIP
		Music.Volume--
		Object.FrameTimer++
		if Object.FrameTimer == 2
			Object.FrameTimer = 0
			Object.Frame++
			if Object.Frame == 5
				// Spawn a ring sparkle at random location close to the sign post
				Rand(TempValue0, 48)
				TempValue0 <<= 16
				TempValue0  += Object.XPos
				TempValue0  -= 0x180000

				Rand(TempValue1, 32)
				TempValue1 <<= 16
				TempValue1  += Object.YPos
				TempValue1  -= 0x1C0000
				CreateTempObject(TypeName[Ring Sparkle], 0, TempValue0, TempValue1)
				Object.Timer--
				if Object.Timer <= 0
					if object.SpinRot >= 500
						object.SpinRot = 0
						object.rotIncrease = 0
						if Object[0].Type == TypeName[Blank Object]	// Check if the player already leave by itself
							Object.State = SIGNPOST_BLANK
							if Object.PropertyValue < 3
								PlayMusic(1)
							else
								if Object.OutOfBounds == false
									PlayMusic(1)
								end if
							end if

							Object[30].Type = TypeName[ActFinish]
							ActFinish.DrawOrder = 6
							ActFinish.Priority	= PRIORITY_ACTIVE
						else	// Otherwise the signpost will do it itself.
							Player.ControlMode 	= CONTROLMODE_NONE
							Player.Up 			= false
							Player.Down 		= false
							Player.Left 		= false
							Player.JumpPress 	= false
							Player.JumpHold 	= false
							Object.State = SIGNPOST_MOVE_PLAYER
						end if
					end if
				end if

			end if

			if Object.Frame == 9

				object.rotIncrease -= 2
				if object.rotIncrease < 2
					object.rotIncrease = 2
				end if
				// Spawn a ring sparkle at random location close to the sign post
				Rand(TempValue0, 48)
				TempValue0 <<= 16
				TempValue0  += Object.XPos
				TempValue0  -= 0x180000
				
				Rand(TempValue1, 32)
				TempValue1 <<= 16
				TempValue1  += Object.YPos
				TempValue1  -= 0x1C0000
				CreateTempObject(TypeName[Ring Sparkle], 0, TempValue0, TempValue1)
				// Resets the animation cycle
				Object.Frame = 1
			end if

		end if
		// Freezes the player after leaving the screen
		TempValue0   = 40
		TempValue0  += Screen.CenterX
		TempValue0 <<= 16
		TempValue0  += Object.XPos
		if Player.XPos > TempValue0
			Player.XPos		 = TempValue0
			Player.Speed	 = 0
			Player.XVelocity = 0
		end if
		break

	case SIGNPOST_MOVE_PLAYER
		if Object[0].Type == TypeName[Blank Object]
			Object.State = SIGNPOST_BLANK
			if Object.PropertyValue < 3
				PlayMusic(1)
			else
				if Object.OutOfBounds == false
					PlayMusic(1)
				end if
			end if
			Object[30].Type		= TypeName[ActFinish]
			ActFinish.DrawOrder = 6
			ActFinish.Priority	= PRIORITY_ACTIVE
		end if

		if Player.ControlMode == CONTROLMODE_NONE
			Player.Right = true
		end if

		TempValue0   = Screen.CenterX
		TempValue0 <<= 16
		TempValue0  += Object.XPos

		if Player.XPos > TempValue0
			if Object.PropertyValue < 3
				PlayMusic(1)
			else
				if Object.OutOfBounds == false
					PlayMusic(1)
				end if
			end if
			Object.State = SIGNPOST_STOP_PLAYER

			Object[30].Type		= TypeName[ActFinish]
			ActFinish.DrawOrder = 6
			ActFinish.Priority	= PRIORITY_ACTIVE
		else
#platform: Use_Origins
			// Some Mission Mode stuff
			if Player.Animation == ANI_PUSHING
				PlayMusic(1)
				Object.State = SIGNPOST_BLANK
				Object[30].Type = TypeName[ActFinish]
				Object[30].DrawOrder = 6
				Object[30].Priority = PRIORITY_ACTIVE
			end if
#endplatform
		end if
		break

	case SIGNPOST_STOP_PLAYER
		if Player.ControlMode == CONTROLMODE_NONE
			Player.Right = true
		end if
		TempValue0   = 40
		TempValue0  += Screen.CenterX
		TempValue0 <<= 16
		TempValue0  += Object.XPos
		if Player.XPos > TempValue0
			Player.XPos		 = TempValue0
			Player.Speed	 = 0
			Player.XVelocity = 0
		end if
		break

	case SIGNPOST_BLANK
		break
	end switch

#platform: Use_Origins
	if game.playMode == BOOT_PLAYMODE_MISSION
		if Object.State > SIGNPOST_IDLE
			// Origins gives the player invincibility, in case the game stops you close to a hazard
			Player.InvincibleTimer = 80
		end if
	end if
#endplatform
end sub


sub ObjectDraw
	DrawSprite(0)

	if object.state > SIGNPOST_IDLE
		CallFunction(SignPost_GetBoxCenter)
		object.SpinRot += object.rotIncrease
		object.SpinRot &= 0x1FF

		// Rotate the points
		CallFunction(SignPost_GetRotation)
		CallFunction(SignPost_DrawBackFace)
		CallFunction(SignPost_DrawFrontFace)
		CallFunction(SignPost_DrawLeftFace)
		CallFunction(SignPost_DrawRightFace)
		CallFunction(SignPost_DrawTopSideFace)

		TempValue0   = object.ixpos
		TempValue0  -= screen.xoffset
		TempValue0  *= 2
		TempValue0  -= screen.xsize
		TempValue0  -= 2

		TempValue1   = object.iypos
		TempValue1  -= screen.yoffset
		TempValue1  -= 76
		TempValue1  *= 2
		TempValue1  -= 0x70
		Not(TempValue1)
		if object.rotIncrease != 0
			MatrixTranslateXYZ(MAT_WORLD, TempValue0, TempValue1, 0x150)
			Draw3DScene()
		else
			DrawSprite(5)
		end if
	else
		DrawSprite(1)
	end if
end sub


sub ObjectStartup
	if Stage.PlayerListPos == PLAYER_SONIC_A // PLAYER_SONIC in origins
		LoadSpriteSheet("Global/Items2.gif")
	end if
	if Stage.PlayerListPos == PLAYER_TAILS_A // PLAYER_TAILS in origins
		LoadSpriteSheet("Global/Items2_t.gif")
	end if
#platform: Use_Origins
	// the file name suffixes are lowercase in every other script, but for some reason they're uppercase here in this script only? sure ig..?
	if Stage.PlayerListPos == PLAYER_KNUCKLES
		LoadSpriteSheet("Global/Items2_K.gif")
	end if
	if Stage.PlayerListPos == PLAYER_AMY
		LoadSpriteSheet("Global/Items2_A.gif")
	end if
#endplatform

	SpriteFrame(-12, -12, 24, 24, 0, 150)		// #0 - Sign Post
	SpriteFrame(-24, -44, 48, 32, 34, 99)		// #1 - Sign Post Head (Eggman)
	SpriteFrame(-16, -44, 32, 32, 1, 183)		// #2 - Sign Post Head (Flip 1)
	SpriteFrame(-4, -44, 8, 32, 25, 150)		// #3 - Sign Post Head (Flip 2)
	SpriteFrame(-16, -44, 32, 32, 1, 216)		// #4 - Sign Post Head (Flip 3)
	SpriteFrame(-24, -44, 48, 32, 34, 132)		// #5 - Sign Post Head (Player)
	SpriteFrame(-16, -44, 32, 32, 1, 183)		// #6 - Sign Post Head (Flip 1)
	SpriteFrame(-4, -44, 8, 32, 25, 150)		// #7 - Sign Post Head (Flip 2)
	SpriteFrame(-16, -44, 32, 32, 1, 216)		// #8 - Sign Post Head (Flip 3)

	SpriteFrame(-4, -44, 8, 4, 25, 150)		// #9 - Sign Post Head (Flip 3)

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Sign Post]
			Object[ArrayPos0].SpinRot = 248

			// If needed, let's extend the stage a bit by repeating the last column of chunks
			if Object[ArrayPos0].PropertyValue < 2
				TempValue0  = TileLayer[0].XSize
				TempValue0 -= 2

				TempValue1   = TileLayer[0].XSize
				TempValue2   = Object[ArrayPos0].iYPos
				TempValue2  += 64
				TempValue2 >>= 7

				GetTileLayerEntry(TempValue3, 0, TempValue0, TempValue2)
				SetTileLayerEntry(TempValue3, 0, TempValue1, TempValue2)

				TempValue0++
				TempValue1++
				GetTileLayerEntry(TempValue3, 0, TempValue0, TempValue2)
				SetTileLayerEntry(TempValue3, 0, TempValue1, TempValue2)

				if Object[ArrayPos0].PropertyValue == UPDATE_TILELAYER_2
					TempValue0--
					TempValue1--
					TempValue2--
					GetTileLayerEntry(TempValue3, 0, TempValue0, TempValue2)
					SetTileLayerEntry(TempValue3, 0, TempValue1, TempValue2)

					TempValue0++
					TempValue1++
					GetTileLayerEntry(TempValue3, 0, TempValue0, TempValue2)
					SetTileLayerEntry(TempValue3, 0, TempValue1, TempValue2)
				end if
			end if
		end if
		ArrayPos0++
	loop
	
	3DScene.NoVertices = 0
	3DScene.NoFaces    = 0
	
	// reset matrix
	MatrixTranslateXYZ(MAT_WORLD, 0, 0, 0)
	MatrixTranslateXYZ(MAT_VIEW, 0, 0, 0)
	MatrixRotateZ(MAT_WORLD, 0)
	ArrayPos0 = 0
	ArrayPos1 = 0

	// Face corner setup
	while ArrayPos0 < 32
		// Top Left
		FaceBuffer[ArrayPos1].a  = ArrayPos0
		// Top Right
		FaceBuffer[ArrayPos1].b  = ArrayPos0
		FaceBuffer[ArrayPos1].b += 2
		// Bottom Left
		FaceBuffer[ArrayPos1].c  = ArrayPos0
		FaceBuffer[ArrayPos1].c += 1
		// Bottom Right
		FaceBuffer[ArrayPos1].d  = ArrayPos0
		FaceBuffer[ArrayPos1].d += 3
	
		ArrayPos1++
		ArrayPos0 += 4
	loop

	// Face rendering
	FaceBuffer[0].Flag  = FACE_TEXTURED_3D

	FaceBuffer[1].Flag  = FACE_TEXTURED_3D

	FaceBuffer[2].Flag  = FACE_COLOURED_3D
	faceBuffer[2].Color = 0xFF303030

	FaceBuffer[3].Flag  = FACE_COLOURED_3D
	faceBuffer[3].Color = 0xFFE0E0E0

	FaceBuffer[4].Flag  = FACE_TEXTURED_3D

	FaceBuffer[5].Flag  = FACE_TEXTURED_3D

	FaceBuffer[6].Flag  = FACE_TEXTURED_3D
	FaceBuffer[7].Flag  = FACE_TEXTURED_3D

	// account for all vertices and faces
	3DScene.NoVertices += 32
	3DScene.NoFaces    += 8
	
	// Back Texture
	// side of the box
	VertexBuffer[0].u = 25
	VertexBuffer[0].v = 182
	VertexBuffer[1].u = 33
	VertexBuffer[1].v = 182
	VertexBuffer[2].u = 33
	VertexBuffer[2].v = 153
	VertexBuffer[3].u = 25
	VertexBuffer[3].v = 153

	// Front Texture
	// uses solid color
	VertexBuffer[4].u = 25
	VertexBuffer[4].v = 182
	VertexBuffer[5].u = 33
	VertexBuffer[5].v = 182
	VertexBuffer[6].u = 33
	VertexBuffer[6].v = 153
	VertexBuffer[7].u = 25
	VertexBuffer[7].v = 153

	// Left Texture
	// Front of the box
	VertexBuffer[16].u = 82
	VertexBuffer[16].v = 131
	VertexBuffer[17].u = 34
	VertexBuffer[17].v = 131
	VertexBuffer[18].u = 34
	VertexBuffer[18].v = 99
	VertexBuffer[19].u = 82
	VertexBuffer[19].v = 99

	// Right Texture
	// Back of the box
	VertexBuffer[20].u = 34
	VertexBuffer[20].v = 164
	VertexBuffer[21].u = 34
	VertexBuffer[21].v = 132
	VertexBuffer[22].u = 82
	VertexBuffer[22].v = 132
	VertexBuffer[23].u = 82
	VertexBuffer[23].v = 164

	// Signpost top
	VertexBuffer[24].u = 25
	VertexBuffer[24].v = 153
	VertexBuffer[25].u = 33
	VertexBuffer[25].v = 153
	VertexBuffer[26].u = 33
	VertexBuffer[26].v = 150
	VertexBuffer[27].u = 25
	VertexBuffer[27].v = 150
	
	VertexBuffer[28].u = 25
	VertexBuffer[28].v = 153
	VertexBuffer[29].u = 33
	VertexBuffer[29].v = 153
	VertexBuffer[30].u = 33
	VertexBuffer[30].v = 150
	VertexBuffer[31].u = 25
	VertexBuffer[31].v = 150
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			CheckResult = Object.PropertyValue
			break
		case 0 // Type
			CheckResult = Object.PropertyValue
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			Object.PropertyValue = Editor.VariableValue
			break
		case 0 // Type
			Object.PropertyValue = Editor.VariableValue
			break
		end switch
	end if
end sub


sub RSDKDraw
	DrawSprite(0)
	DrawSprite(1)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Items2.gif")
	SpriteFrame(-12, -12, 24, 24, 0, 150)		// #0 - Sign Post
	SpriteFrame(-24, -44, 48, 32, 34, 99)		// #1 - Sign Post Head (Eggman)


	AddEditorVariable("Type")
	SetActiveVariable("Type")
	AddEnumVariable("Updates TileLayer (1)", 0)
	AddEnumVariable("Updates TileLayer (2)", 1)
	AddEnumVariable("Normal", 2)
	AddEnumVariable("Normal (Handles Multiple SignPosts)", 3)
end sub
